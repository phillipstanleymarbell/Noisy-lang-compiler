/*
 *	Lexical elements
 */
reservedIdentifiers		::=	":" | "," | "@" | "o<" | "~" | "<" | "<=" | ">" | ">=" | "==" | "<->"
					| "**" | "*" | "/" | "+" | "-" | "<<" | ">>" | "|" | "++" | "--"
					| "-" | "English" | "constant" | "invariant" | "signal"
					| "sensor" | "name" | "symbol" | "derivation" | "none"
					| "dot" | "cross" | "derivative" | "integral" | "interface"
					| "i2c" | "spi" | "analog" | "write" | "read" | "delay" | "range"
					| "erasuretoken" | "uncertainty" | "accuracy" | "precision"
					| "piecewise" | "case" | "otherwise"
					| "GaussianDistribution" | "LaplacianDistribution" | "StudentTDistribution" | "BernoulliDistribution" | "BinomialDistribution" 
					| "PoissonDistribution" | "NegativeBinomialDistribution" | "BetaBinomialDistribution" | "ExponentialDistribution"
					| "GammaDistribution" | "MultinomialDistribution" | "BetaDistribution" | "LogitNormalDistribution" | "DirichletDistribution"
					| "CauchyDistribution" | "LogNormalDistribution" | "ParetoDistribution" | "BetaPrimeDistribution" | "StudentZDistribution" 
					| "WeibullDistribution" | "ErlangDistribution" | "MaxwellDistribution" | "FermiDiracDistribution" | "FisherZDistribution" 
					| "LogSeriesDistribution" | "GumbelDistribution" | "RayleighDistribution" | "GibratDistribution" | "PearsonIIIDistribution"
					| "ExtremeValueDistribution"| "FDistribution" | "XiDistribution" | "XiSquaredDistribution" | "to" | "bits" | "><"
					| "dimensionless" | "include" | "(" | ")" | "{" | "}" | ";" | ".." | "->" .

zeroToNine			=	"0-9" .
oneToNine			=	"1-9" .
radix				=	oneToNine {zeroToNine} "r" .
charConst			=	"'" character "'" .
integerConst			::=	["+" | "-"] [radix] ("0" | oneToNine {zeroToNine}) | charConst .
drealConst			=	("0" | oneToNine {zeroToNine}) "." {zeroToNine} .
erealConst			=	(drealConst | integerConst) ("e" | "E") integerConst .
realConst			::=	["+" | "-"] (drealConst | erealConst) .
stringConst			::=	'"' {character} '"' .
idchar				=	char-except-rsvopseptoken .
identifier			::=	(idchar-except-zeroToNine) {idchar} .

/*
 *	Syntactic elements. The language grammar is purposefully stricter than it needs
 *	to be based purely on the standard operator precedence rules: In many places
 *	the grammar purposefully requires a `factor` when a `term` would be acceptable
 *	based on operator precedence. For example, for `constantDefinition` production,
 *	rather than
 *
 *		constantDefinition	::=	identifier ":" "constant" "=" numericTerm [unitExpression] ";" .
 *
 *	the language requires the constant value to be a `factor` (a single constant
 *	or a term/expression in parentesis):
 *
 *		constantDefinition	::=	identifier ":" "constant" "=" numericFactor [unitFactor] ";" .
 */
newtonDescription		::=	ruleList .
ruleList			::=	{rule} .
rule				::=	constantDefinition | invariantDefinition | baseSignalDefinition | sensorDefinition .
constantDefinition		::=	identifier ":" "constant" "=" numericFactor [unitFactor] ";" .
invariantDefinition		::=	identifier ":" "invariant" parameterTuple  "=" "{" [constraintList] "}" .
baseSignalDefinition		::=	identifier ":" "signal" [subdimensionTuple] "=" "{" [nameStatement] [signalUncertaintyStatement] [sensorStatement] symbolStatement derivationStatement "}" .
sensorDefinition		::=	identifier ":" "sensor" parameterTuple "=" "{" sensorPropertyList "}" .
nameStatement			::=	"name" "=" stringConst languageSetting ";" .
signalUncertaintyStatement	::= "uncertainty" "=" distribution [parameterTuple] ";" .
sensorStatement			::=	"sensor" "=" identifier ";" .
symbolStatement			::=	"symbol" "=" identifier ";" .
derivationStatement		::=	"derivation" "=" ("none" | "dimensionless" | quantityExpression) ";" .
subdimensionTuple		::=	"(" identifier ":" numericExpression "to" numericExpression ")" .
parameterTuple			::=	"(" parameter {"," parameter} ")" .
parameter			::=	identifier ":" (identifier | numericExpression) {"[" integerConst "]"} .
constraintList			::=	[constraint] {"," constraint} .
constraint			::=	quantityExpression comparisonOperator quantityExpression | identifier callParameterTuple | piecewiseConstraint .
callParameterTuple		::=	"(" identifier {"," identifier} ")" .
piecewiseConstraint		::=	"piecewise" "{" caseStatementList "}" .
caseStatementList		::=	caseStatement {"," caseStatement} .
caseStatement			::=	("case" constraint | "otherwise" )"->" "{" constraintList "}" .
quantityExpression		::=	quantityTerm {lowPrecedenceOperator quantityTerm} .
quantityTerm			::=	[unaryOp] quantityFactor {highPrecedenceQuantityOperator quantityFactor} .

/*
 *	The way we have the optional [exponentiationOperator quantityFactor] both after `quantity` 
 *	as well as after `"(" quantityExpression ")"` is a bit awkward but represents what we actually want.
 *	It is partly a result of the exponentiation operator having even higher precedence than the "highPrecedenceOperator" ('*' and '/')
 */
quantityFactor			::=	quantity [exponentiationOperator numericFactor]						|
						functionalOperator {functionalOperator} quantityFactor quantityFactor			|
						distribution "(" quantityExpression {"," quantityExpression} ")"		|
						transcendental "(" quantityExpression ")"			|
						"(" quantityExpression ")" [exponentiationOperator numericFactor]			|
						"{" quantityExpression {"," quantityExpression} "}" .
/*
 *	The following needs to be
 *
 *			numericConst | (identifier {"[" numericFactor "]"}) .
 *
 *	and not
 *
 *			numericFactor | (identifier {"[" numericFactor "]"}) .
 *
 *	because the latter would lead to an ambiguity in parsing quantityFactor
 */
quantity			::=	numericConst | (identifier ["[" numericFactor "]"]) .
highPrecedenceQuantityOperator	::=	highPrecedenceOperator | vectorOp | "><" .
lowPrecedenceOperator		::=	"+" | "-" .
highPrecedenceOperator		::=	"*" | "/" .
exponentiationOperator		::=	"**" .
vectorOp			::=	"dot" | "cross" .
functionalOperator		::=	"derivative" | "integral" .
comparisonOperator		::=	"o<" | "~" | "<" | "<=" | ">" | ">=" | "==" | "<->" .
languageSetting			::=	"English" .
unitExpression			::=	unitTerm .
unitTerm			::=	unitFactor {highPrecedenceOperator unitFactor} .
unitFactor			::=	(unit [exponentiationOperator numericFactor]) | "(" unitExpression ")" .
unit				::=	identifier .
numericExpression		::=	numericTerm {lowPrecedenceOperator numericTerm} .
numericTerm			::=	numericFactor {highPrecedenceOperator numericFactor} .
numericFactor			::=	(numericConst [exponentiationOperator numericConst]) | "(" numericExpression ")" .
numericConst			::=	integerConst | realConst .
sensorPropertyList		::=	sensorProperty {"," sensorProperty} .
sensorProperty			::=	rangeStatement | uncertaintyStatement | erasureValueStatement | accuracyStatement | precisionStatement | sensorInterfaceStatement .
sensorInterfaceStatement	::=	"interface" identifier ["@" numericFactor "bits"] "==" sensorInterfaceType [parameterTuple ["{" sensorInterfaceCommandList "}"]] .
sensorInterfaceType		::=	"i2c" | "spi" | "analog" .
sensorInterfaceCommandList	::=	sensorInterfaceCommand ";" {sensorInterfaceCommand ";"} .
sensorInterfaceCommand		::=	readRegisterCommand | writeRegisterCommand | delayCommand | arithmeticCommand .
readRegisterCommand		::=	identifier [":=" | "="] "read" ["[" numericExpression "]" ","] numericExpression .
writeRegisterCommand		::=	"write" numericExpression "," numericExpression .
delayCommand			::=	"delay" numericExpression .
arithmeticCommand		::=	identifier [":=" | "="] expression .
rangeStatement			::=	"range" identifier "==" "[" numericFactor [unitFactor] "," numericFactor [unitFactor] "]" .
uncertaintyStatement		::=	"uncertainty" identifier "==" factor [unitFactor] .
erasureValueStatement		::=	"erasuretoken" identifier "==" numericFactor [unitFactor] .
accuracyStatement		::=	"accuracy" identifier "==" numericConstTupleList .
precisionStatement		::=	"precision" identifier "==" numericConstTupleList .
numericConstTupleList		::=	"{" numericConstTuple {"," numericConstTuple} "}" .
numericConstTuple		::=	"(" numericFactor [unitFactor] "," numericFactor ")" .
expression			::=	term {lowPrecedenceBinaryOp term} .
term				::=	[unaryOp] factor ["++" | "--"] {highPrecedenceBinaryOp factor} .
factor				::=	numericConst | "(" expression ")" | distributionFactor | identifier ["[" yy numericExpression "]"] .
distributionFactor		::=	distribution parameterValueList .
parameterValueList		::=	"(" identifier ":" expression {"," identifier ":" expression} ")" .
distribution			::=	"Gaussian" | "Laplacian" | "StudentT" | "Bernoulli" | "Binomial" 
					| "Poisson" | "NegativeBinomial" | "BetaBinomial" | "Exponential"
					| "Gamma" | "Multinomial" | "Beta" | "LogitNormal" | "Dirichlet"
					| "Cauchy" | "LogNormal" | "Pareto" | "BetaPrime" | "StudentZ" 
					| "Weibull" | "Erlang" | "Maxwell" | "FermiDirac" | "FisherZ" 
					| "LogSeries" | "Gumbel" | "Rayleigh" | "Gibrat" | "PearsonIII"
					| "ExtremeValue" | "F" | "Xi" | "XiSquared" | "Unconstrained" .
transcendental			::= "sin" | "cos" | "tan" | "cotan" | "sec"  | "cosec"
						| "arcsin" | "arccos" | "arctan" | "arccotan" | "arcsec"  | "arccosec"
						| "sinh" | "cosh" | "tanh" | "cotanh" | "sech"  | "cosech"
						| "arcsinh" | "arccosh" | "arctanh" | "arccotanh" | "arcsech"  | "arccosech"
						| "exp" | "sqrt"
						| "ln" | "log10" | "log2" .
highPrecedenceBinaryOp		::=	"*" | "/" | "%" | "**" .
lowPrecedenceBinaryOp		::=	"+" | "-" | ">>" | "<<" | "|" .
unaryOp				::=	"-"  | "+" .
